// The Nature of Code
// Daniel Shiffman
// http://natureofcode.com

//MAIN
// All of this stuff should go into a Cannon class
float angle = -PI/2;
PVector position = new PVector(50, 50);
boolean shot = false;

CannonBall ball;

void setup() {
  size(300, 300);
  ball = new CannonBall(position.x, position.y);
}

void draw() {
  background(255); 
  
 if (shot) 
 {
    PVector gravity = new PVector(0, 0.2);
    ball.applyForce(gravity);
    ball.update();
  }
  ball.display();

  if (ball.position.y > height+50) {
    ball = new CannonBall(position.x, position.y);  
    shot = false;
  }
}

void keyPressed() {
  
  if (key == ' ') {
    shot = true;
    PVector force = PVector.fromAngle(angle);
    force.mult(10);
    ball.applyForce(force);
  }
}

//CLASS 

// The Nature of Code
// Daniel Shiffman
// http://natureofcode.com

class CannonBall { 
  // All of our regular motion stuff
  PVector position;
  PVector velocity;
  PVector acceleration;

  // Size
  float r = 8;
  
  float topspeed = random(1,5);

  CannonBall(float x, float y) {
    position = new PVector(x,y);
    velocity = new PVector();
    acceleration = new PVector();
  } 

  // Standard Euler integration
  void update() { 
    velocity.add(acceleration);
    velocity.limit(topspeed);
    position.add(velocity);
    acceleration.mult(0);
  }

  void applyForce(PVector force) {
    acceleration.add(force);
  }

  
  void display() { 
    stroke(0);
    strokeWeight(2);
    pushMatrix();
    translate(position.x,position.y);
    ellipse(0,0,r*2,r*2);
    popMatrix();
  }
}
